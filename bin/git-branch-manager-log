#!/usr/bin/env zsh

set -eu -o pipefail

function git_branch_manager_log {
    local args=()
    local flags=()
    local extra_flags=()
    while [[ $# -gt 0 ]]; do
        case $1 in
            (--)
                extra_flags=("$@")
                break
            ;;
            (-*) flags+=$1 ;;
            (*) args+=$1 ;;
        esac
        shift
    done

    if [[ ${#args} -gt 1 ]]; then
        echo 'Cannot pass multiple arguments to git-branch-manager-log' >&2
        return 1
    fi

    local target=${args[1]:-HEAD}

    local start=HEAD~10

    local base="$(git branch-manager --base ${args[1]:-})"
    if [[ -n $base ]]; then
        start="$(git merge-base $base $target)"
    fi

    local range="${start}^..${target}"
    if ! git show "${start}^" &> /dev/null; then
        range=--root
    fi

    exec git log --first-parent $flags $range $extra_flags
}

git_branch_manager_log "$@"
