#!/usr/bin/env zsh

set -eu -o pipefail

usage="
Usage: git files [--status] [-s|--staged] [-m|--modified] [COMMIT]

Show the files changed in the given commit. At most one of --staged,
--modified, and COMMIT may be provided.

Parameters:
--status            Show the status of the file (see the man page for
                    'git diff --diff-filter' for more information)
-s, --staged        Show currently staged files
-m, --modified      Show currently modified files
COMMIT              Specify the commit to inspect. Defaults to HEAD.
"

function abort { print "$@" >&2; exit 1 }
function abort_help { abort -f '%s\n' "$@" $usage }

function parse_args {
    local show_staged=0
    local show_modified=0
    local show_status=0
    local pos_args=()

    for arg in "$@"; do
        case $arg in
            (-s|--staged) show_staged=1 ;;
            (-m|--modified) show_modified=1 ;;
            (--status) show_status=1 ;;
            (-*) abort_help ;;
            (*) pos_args+=$1 ;;
        esac
    done

    if [[ ${#pos_args} -gt 1 ]]; then
        abort_help
    fi

    local target commit=
    case "${show_staged}:${show_modified}:${#pos_args}" in
        (1:0:0)
            target=staged
        ;;
        (0:1:0)
            target=modified
        ;;
        (0:0:1)
            target=commit
            commit=${pos_args[1]}
        ;;
        (0:0:0)
            target=commit
            commit=HEAD
        ;;
        (*)
            abort "At most one of --staged, --modified, and COMMIT may be specified"
        ;;
    esac

    args=(
        [target]=$target
        [commit]=$commit
        [with_status]=$show_status
    )
}

function git_files {
    typeset -A args
    parse_args "$@"

    local flags=()
    if [[ $args[with_status] == 1 ]]; then
        flags+='--name-status'
    else
        flags+='--name-only'
    fi

    case $args[target] in
        (staged)
            git diff $flags --staged
        ;;
        (modified)
            git diff $flags
        ;;
        (commit)
            git diff-tree $flags --no-commit-id -r $args[commit]
        ;;
    esac
}

git_files "$@"
