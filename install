#!/usr/bin/env zsh

set -ex

cd "$(dirname "${0:A}")"

# `link foo bar` creates a symlink from `foo` to `bar`.
# Note: this is the opposite of `ln`, which links the
# other direction
function link {
    local target=$1
    local src=$2
    mkdir -p "$(dirname "$target")"
    rm -rf "$target"
    ln -sf "$src" "$target"
}

############### HOME dotfiles ###############

files=(
    bin
    gitconfig
    inputrc
    vim
    vimrc
    zsh
    zshrc
)

for file in "${files[@]}"; do
    link "${HOME}/.${file}" "${PWD}/${file}"
done

############### Upstream completion scripts ###############

function download_file {
    local src=$1
    local dest=$2
    shift 2

    if [[ ! -f $dest ]]; then
        mkdir -p "$(dirname $dest)"
        curl -o $dest $src "$@"
    fi
}

function gh_url {
    local repo=$1
    local tag=$2
    local path=$3
    echo "https://raw.githubusercontent.com/${repo}/${tag}/${path}"
}

download_file "$(gh_url 'git/git' 'v2.24.3' 'contrib/completion/git-completion.bash')" ~/.local/bin/git-completion.bash
download_file "$(gh_url 'git/git' 'v2.24.3' 'contrib/completion/git-completion.zsh')" ~/.local/zsh/_git

download_file "$(gh_url 'docker/cli' 'v20.10.2' 'contrib/completion/zsh/_docker')" ~/.local/zsh/_docker
download_file "$(gh_url 'docker/compose' '1.27.4' 'contrib/completion/zsh/_docker-compose')" ~/.local/zsh/_docker-compose

############### Brew dependencies ###############

if ! type brew &> /dev/null; then
    /usr/bin/ruby -e "$(curl -fsSL https://raw.githubusercontent.com/Homebrew/install/master/install)"
fi

brew bundle

############### Application settings ###############

# Maccy
defaults write org.p0deje.Maccy pasteByDefault -string '1'
defaults write org.p0deje.Maccy popupPosition -string 'center'
defaults write org.p0deje.Maccy hideFooter -string '1'
defaults write org.p0deje.Maccy hideTitle -string '1'
defaults write org.p0deje.Maccy maxMenuItems -string '10'
defaults write org.p0deje.Maccy KeyboardShortcuts_popup -string '{"carbonModifiers":4096,"carbonKeyCode":9}'
defaults write org.p0deje.Maccy enabledPasteboardTypes -array 'public.utf8-plain-text'

# Sublime Text
link "${HOME}/Library/Application Support/Sublime Text 3/Packages/User" "${PWD}/sublime"
